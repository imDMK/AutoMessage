plugins {
    id("java-library")

    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("net.minecrell.plugin-yml.bukkit") version "0.6.0"
    id("checkstyle")
}

group = "com.github.imdmk.automessage"
version = "1.0.2"

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()

    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://storehouse.okaeri.eu/repository/maven-public/" }
    maven { url = "https://repo.panda-lang.org/releases" }
    maven { url = "https://repo.eternalcode.pl/releases" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.5-R0.1-SNAPSHOT")

    implementation("eu.okaeri:okaeri-configs-yaml-snakeyaml:5.0.8")
    implementation("eu.okaeri:okaeri-configs-serdes-commons:5.0.0-beta.5")

    implementation("net.kyori:adventure-platform-bukkit:4.4.0")
    implementation("net.kyori:adventure-text-minimessage:4.19.0")

    implementation("dev.rollczi:litecommands-bukkit:3.9.7")
    implementation("dev.rollczi:litecommands-annotations:3.9.7")

    implementation("com.eternalcode:multification-bukkit:1.2.1")
    implementation("com.eternalcode:multification-okaeri:1.1.4")
    implementation("com.eternalcode:gitcheck:1.0.0")

    implementation("org.bstats:bstats-bukkit:3.1.0")

    testImplementation platform("org.junit:junit-bom:5.10.1")
    testImplementation("org.junit.jupiter:junit-jupiter:5.12.2")
}

bukkit {
    name = "AutoMessage"
    version = "${project.version}"
    apiVersion = "1.17"
    main = "com.github.imdmk.automessage.AutoMessagePlugin"
    author = "imDMK"
    description = "Advanced plugin for sending automatic chat, bossbar, and title messages to players."
    website = "https://github.com/imDMK/AutoMessage"
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = "10.21.0"
    configFile = file("checkstyle.xml")
}

shadowJar {
    archiveFileName.set("${project.name} v${project.version}.jar")

    dependsOn("checkstyleMain")
    dependsOn("checkstyleTest")
    dependsOn("test")

    exclude(
            "org/intellij/lang/annotations/**",
            "org/jetbrains/annotations/**",
            "META-INF/**",
    )

    def prefix = "com.github.imdmk.automessage.lib"
    [
            "net.kyori",
            "dev.rollczi",
            "org.yaml",
            "org.bstats",
            "org.json",
            "com.eternalcode",
            "eu.okaeri",
    ].each { lib ->
        relocate(lib, "${prefix}.${lib}")
    }
}